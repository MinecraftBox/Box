buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        maven { url 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        // MCGradle 2.0.10
        classpath 'com.github.asbyth:MCGradle:09f84c1c42906c19e0f7902512a9215481279b4e'
        }
    }

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

def kotlinVersion = '1.3.72'

apply plugin: 'club.ampthedev.mcgradle'
apply plugin: 'maven-publish'

sourceCompatibility = targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'

mcgradle {
    version '1.8.9'
    clientMainClass = 'net.minecraft.launchwrapper.Launch'
    args += ["--tweakClass", "dev.minecraftbox.launch.BoxTweaker"]
}

group 'dev.minecraftbox'
version '0.1-DEV'

mcgradle.boxVersion = project.version

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://libraries.minecraft.net' }
    maven { url 'https://repo.spongepowered.org/maven' }
}

// Jitpack compatibility
if (!new File(projectDir, 'src/minecraft').exists()) {
    tasks.compileKotlin.dependsOn tasks.setupCIWorkspace
    tasks.compileJava.dependsOn tasks.setupCIWorkspace
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation('net.minecraft:launchwrapper:1.12') {
        exclude module: 'lwjgl'
    }
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
}

jar {
    dependsOn configurations.compile
    from { configurations.embed.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}